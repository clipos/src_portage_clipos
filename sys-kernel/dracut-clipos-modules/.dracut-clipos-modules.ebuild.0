# Copyright Â© 2019 ANSSI. All rights reserved.
# Distributed under the terms of the GNU General Public License v2

EAPI=7

inherit clipos-product-info clipos-utils

DESCRIPTION="CLIP OS specific dracut modules"
HOMEPAGE="https://clip-os.org"

LICENSE="LGPL-2.1+"
SLOT="0"
KEYWORDS="amd64"
IUSE="clipos_instrumentations_instrumented-initramfs"
IUSE+=" clipos_instrumentations_initramfs-no-require-tpm"
IUSE+=" clipos_instrumentations_initramfs-no-tpm-lockout"

RDEPEND="sys-kernel/dracut"

S="${WORKDIR}"

src_install() {
	modules=(
		"10clipos-core-verity"
		"11clipos-core-state"
		"80clipos-check-state"
		"90clipos-boot-failed"
	)

	moddir="/usr/lib/dracut/modules.d"
	dodir "${moddir}"

	for module in ${modules[@]}; do
		cp --recursive --preserve=mode,timestamp \
			"${FILESDIR}/${module}" "${ED}/${moddir}/${module}"
	done

	if use clipos_instrumentations_instrumented-initramfs; then
		# Enable debug root shell in initramfs if something fails during bootup
		cat "${FILESDIR}/90clipos-boot-failed/boot-failed.service.instrumented" \
			>> "${ED}/${moddir}/90clipos-boot-failed/boot-failed.service"
	fi

	# The volume group name is required by the script mounting the stateful
	# partition of the Core (see a bit further down):
	readonly VG_NAME="$(get_vgname)"

	# Require a TPM 2.0 by default to store the keyfile to the encrypted Core
	# state partition:
	REQUIRE_TPM=true
	if use clipos_instrumentations_initramfs-no-require-tpm; then
		REQUIRE_TPM=false
	fi
	readonly REQUIRE_TPM

	# Enable dictionary attack protection (see TPM documentation about "noDA"
	# attribute) by default:
	BRUTEFORCE_LOCKOUT=true
	if use clipos_instrumentations_initramfs-no-tpm-lockout; then
		BRUTEFORCE_LOCKOUT=false
	fi
	readonly BRUTEFORCE_LOCKOUT

	# Replace placeholder values in the script mounting the Core state partition:
	export VG_NAME REQUIRE_TPM BRUTEFORCE_LOCKOUT
	replace_placeholders "$ED/$moddir/11clipos-core-state/mount-core-state.sh"
}
