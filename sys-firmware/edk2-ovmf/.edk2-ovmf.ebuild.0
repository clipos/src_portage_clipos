# Copyright 1999-2018 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2

EAPI=6

PYTHON_REQ_USE="sqlite"
PYTHON_COMPAT=( python3_6 )

inherit eutils python-any-r1

DESCRIPTION="UEFI firmware for 64-bit x86 virtual machines"
HOMEPAGE="https://github.com/tianocore/edk2"

REQUIRED_USE="
	${PYTHON_REQUIRED_USE}
"
DEPEND="
	>=dev-lang/nasm-2.12
	>=sys-power/iasl-20160729
	${PYTHON_DEPS}
"

if [[ ${PV} == "9999" ]] ; then
	inherit git-r3
	EGIT_REPO_URI="https://github.com/tianocore/edk2"
	EGIT_BRANCH="master"
	KEYWORDS="~amd64"
else
	RELEASE_NAME="edk2-stable$PV"
	SRC_URI="https://github.com/tianocore/edk2/archive/${RELEASE_NAME}.tar.gz -> ${P}.tar.gz"
	OPENSSL_PV="1.1.1b"
	OPENSSL_P="openssl-${OPENSSL_PV}"
	SRC_URI+=" mirror://openssl/source/${OPENSSL_P}.tar.gz"
	KEYWORDS="amd64"
fi

LICENSE="BSD-2"
SLOT="0"

pkg_setup() {
	 python-any-r1_pkg_setup
}

src_unpack() {
	if [[ ${PV} == 9999 ]]; then
		git-r3_src_unpack
	else
		unpack ${A}
		mv "edk2-${RELEASE_NAME}" "${P}"
		local openssllib="${S}/CryptoPkg/Library/OpensslLib/openssl"
		rmdir "${openssllib}" && \
			mv "${WORKDIR}/${OPENSSL_P}" "${openssllib}" || die
	fi
}

src_compile() {
	TARGET_ARCH=X64
	TARGET_NAME=RELEASE
	TARGET_TOOLS=GCC5

	emake ARCH=${TARGET_ARCH} -C BaseTools -j1

	. ./edksetup.sh

	# Enable Secure Boot, require SMM support and add TPM 2.0 support
	./OvmfPkg/build.sh \
		-a "${TARGET_ARCH}" -b "${TARGET_NAME}" -t "${TARGET_TOOLS}" \
		-p "OvmfPkg/OvmfPkg${TARGET_ARCH}.dsc" \
		-D FD_SIZE_2MB -D SECURE_BOOT_ENABLE -D SMM_REQUIRE -D TPM2_ENABLE \
		|| die "OvmfPkg/build.sh failed"
}

src_install() {
	local builddir="Build/OvmfX64/${TARGET_NAME}_${TARGET_TOOLS}/FV"

	insinto /usr/share/${PN}
	doins "${builddir}"/OVMF{,_CODE,_VARS}.fd
}
